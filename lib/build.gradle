plugins {
    id 'com.android.library'
    id 'maven-publish'
}

android {
    namespace = 'org.beyka.tiffbitmapfactory'
    compileSdk = 35
    ndkVersion = '28.0.12916984'

    defaultConfig {
        minSdk = 21
        targetSdk = 35
        versionCode 987
        versionName "0.9.9.0"

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11 -fvisibility=hidden"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    buildFeatures {
        buildConfig = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.22.1"
        }
    }

    lintOptions {
        abortOnError = false
    }

    // necessary from AGP8+ to create the `release` component
    // accessed in `afterEvaluate` for publication via `MavenPublication`
    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
        multipleVariants {
            withSourcesJar()
            withJavadocJar()
            allVariants()
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
}

project.afterEvaluate {
    print("Components: " + components*.name)

    publishing {
        publications {
            release(MavenPublication) {
                from components.findByName('release')
            }
        }
    }
}
